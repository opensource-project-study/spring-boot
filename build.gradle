plugins {
	id "base"
	id "org.jetbrains.kotlin.jvm" apply false // https://youtrack.jetbrains.com/issue/KT-30276
	id "io.spring.nohttp" version "0.0.11"
}

description = "Spring Boot Build"

defaultTasks 'build'

nohttp {
	allowlistFile = project.file("src/nohttp/allowlist.lines")
	source.exclude "**/bin/**"
	source.exclude "**/build/**"
	source.exclude "**/out/**"
	source.exclude "**/target/**"
	source.exclude "**/.settings/**"
	source.exclude "**/.classpath"
	source.exclude "**/.project"
	source.exclude "spring-boot-project/spring-boot-tools/spring-boot-buildpack-platform/src/test/resources/org/springframework/boot/buildpack/platform/docker/export.tar"
}

check {
	dependsOn checkstyleNohttp
}

allprojects {
	group "org.springframework.boot"

	repositories {
		// 优先从maven的本地仓库加载依赖，如果本地仓库中没有，从远程仓库下载到GRADLE_USER_HOME路径下，并从改路径处引用依赖
		// 注意：从远程仓库下载后，并不会复制到maven的本地仓库中。这一点gradle和maven是有区别的。
		mavenLocal()
		mavenCentral()
		if (version.contains('-')) {
			maven { url "https://repo.spring.io/milestone" }
		}
		if (version.endsWith('-SNAPSHOT')) {
			maven { url "https://repo.spring.io/snapshot" }
		}
	}

	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 0, "minutes"
	}
}

tasks.named("checkstyleNohttp").configure {
	maxHeapSize = "1536m"
}
